dnl
dnl    ettercap -- configure script
dnl
dnl    Copyright (C) ALoR & NaGA
dnl
dnl    This program is free software; you can redistribute it and/or modify
dnl    it under the terms of the GNU General Public License as published by
dnl    the Free Software Foundation; either version 2 of the License, or
dnl    (at your option) any later version.
dnl
dnl    This program is distributed in the hope that it will be useful,
dnl    but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl    GNU General Public License for more details.
dnl
dnl    You should have received a copy of the GNU General Public License
dnl    along with this program; if not, write to the Free Software
dnl    Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
dnl   
dnl    $Id: configure.in,v 1.57 2003/11/02 20:36:35 alor Exp $
dnl

AC_INIT(include/ec.h)

dnl Force autoconf 2.57 
AC_PREREQ(2.57)

dnl Force automake 1.7 or better
dnl AM_INIT_AUTOMAKE(1.7.0)

VERSION=`grep EC_VERSION ./include/ec_version.h | cut -f2 -d"\""`
SB=`./shtool echo -n -e %B`
EB=`./shtool echo -n -e %b`
AC_SUBST(SB)
AC_SUBST(EB)
AC_SUBST(VERSION)
EC_MESSAGE(Configuring ettercap $VERSION)

AC_CONFIG_SRCDIR(src)
AM_INIT_AUTOMAKE(ettercap, $VERSION)

AM_CONFIG_HEADER(include/config.h)
dnl AC_CONFIG_HEADERS(include/config.h)

dnl ================
dnl   Check the OS
dnl ================

AC_CANONICAL_HOST

AH_TEMPLATE(OS_LINUX, [compiling under linux])    

AH_TEMPLATE(OS_BSD, [compiling under bsd])
dnl the subtype for BSD only used if there are significant differences
AH_TEMPLATE(OS_BSD_FREE, [compiling under free bsd])
AH_TEMPLATE(OS_BSD_OPEN, [compiling under open bsd])
AH_TEMPLATE(OS_BSD_NET, [compiling under net bsd])

AH_TEMPLATE(OS_DARWIN, [compiling under macosx])

AH_TEMPLATE(OS_SOLARIS, [compiling under solaris])

AH_TEMPLATE(OS_CYGWIN, [compiling under cygwin win32])

dnl initialize the conditionals
AM_CONDITIONAL(LINUX, false)
AM_CONDITIONAL(BSD, false)
AM_CONDITIONAL(DARWIN, false)
AM_CONDITIONAL(SOLARIS, false)
AM_CONDITIONAL(CYGWIN, false)
   
case "$host_os" in
*linux*)
   AC_DEFINE(OS_LINUX)
   AM_CONDITIONAL(LINUX, true)
   OS="LINUX"
   PTHREADLIB="-lpthread"
   ;;
*freebsd*)
   AC_DEFINE(OS_BSD)
   AC_DEFINE(OS_BSD_FREE)
   AM_CONDITIONAL(BSD, true)
   OS="FREEBSD"
   PTHREADLIB="-pthread"
   ;;
*openbsd*)
   AC_DEFINE(OS_BSD)
   AC_DEFINE(OS_BSD_OPEN)
   AM_CONDITIONAL(BSD, true)
   OS="FREEBSD"
   PTHREADLIB="-pthread"
   ;;
*netbsd*)
   AC_DEFINE(OS_BSD)
   AC_DEFINE(OS_BSD_NET)
   AM_CONDITIONAL(BSD, true)
   OS="FREEBSD"
   PTHREADLIB="-pthread"
   ;;
*darwin*)
   AC_DEFINE(OS_DARWIN)
   AM_CONDITIONAL(DARWIN, true)
   OS="DARWIN"
   PTHREADLIB="-lpthread"
   ;;
*solaris*)
   AC_DEFINE(OS_SOLARIS)
   AM_CONDITIONAL(SOLARIS, true)
   OS="SOLARIS"
   PTHREADLIB="-lpthread"
   SOCKETLIB="-lsocket -lnsl"
   ;;
*cygwin*)
   EC_WINDOWS_KERNEL()
   AC_DEFINE(OS_CYGWIN)
   AM_CONDITIONAL(CYGWIN, true)
   OS="CYGWIN $ac_cv_ec_windows_version"
   PTHREADLIB="-lpthread"
   PL_LDFLAGS="-no-undefined"
   ;;
*)
   echo
   echo "NOT A SUPPORTED SYSTEM / OR SYSTEM NOT RECOGNIZED"
   echo
   exit 1
   ;;
esac

AC_SUBST(PL_LDFLAGS)

dnl ======================
dnl   Initialize prefix.
dnl ======================

AC_PREFIX_DEFAULT(/usr/local)

if test "$prefix" = "NONE"; then
   prefix="/usr/local"
fi

AH_TEMPLATE(INSTALL_PREFIX, [where can I find my conf file ?])

AC_DEFINE_UNQUOTED(INSTALL_PREFIX, "${prefix}")

dnl ========================
dnl   Check the C compiler
dnl ========================

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AC_PROG_GCC_TRADITIONAL
EC_GCC_MACRO
AH_TEMPLATE(GCC_VERSION, [gcc version])
gcc_ver=`$CC -dumpversion`
AC_DEFINE_UNQUOTED(GCC_VERSION, "$gcc_ver")

dnl ========================
dnl   Bison/Flex related...
dnl ========================

AC_PROG_YACC
AC_PROG_LEX

dnl ========================
dnl   Libtool related...
dnl ========================

AC_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_SEARCH_LIBS(dlopen,c dl,,)
AC_LIB_LTDL
AC_SEARCH_LIBS(lt_dlopen,c dl ltdl,,)
AC_CHECK_HEADERS(dlfcn.h)
AC_CHECK_HEADERS(ltdl.h)

dnl ======================
dnl   Machine Endianness
dnl ======================

AC_C_BIGENDIAN

dnl =====================
dnl   Check for headers
dnl =====================

AC_HEADER_STDC
AC_HEADER_TIME
AC_HEADER_DIRENT

dnl These are required !!
AC_CHECK_HEADERS(unistd.h stdlib.h signal.h stdarg.h sys/ioctl.h sys/types.h dirent.h errno.h,,
   AC_MSG_WARN(****************************************************);
   AC_MSG_WARN(* REQUIRED !! I cant believe you don't have this !!*);
   AC_MSG_WARN(****************************************************);
   exit 1)

AC_CHECK_HEADERS(fcntl.h ctype.h)
AC_CHECK_HEADERS(sys/types.h stdint.h)
AC_CHECK_HEADERS(sys/time.h sys/utsname.h)
AC_CHECK_HEADERS(termios.h)
AC_CHECK_HEADERS(sys/poll.h poll.h sys/select.h)
dnl nameser NS_GET32 and so on...
EC_NS_GET()

dnl ==================================================================
dnl   Checks for typedefs, structures, and compiler characteristics.
dnl ==================================================================

AC_STRUCT_TM
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_C_CONST

dnl =====================
dnl   Check for libs
dnl =====================


AC_TYPE_SIGNAL
AC_FUNC_VPRINTF
EC_PTHREAD_CHECK()
AC_CHECK_FUNCS(getifaddrs)
AC_CHECK_FUNCS(gettimeofday)
AC_CHECK_FUNCS(vsnprintf)
AC_CHECK_FUNCS(select poll strdup strerror strstr strsignal)
AC_CHECK_FUNCS(uname)
AC_CHECK_FUNCS(daemon)

EC_MESSAGE(Checking for required libraries)

AC_SEARCH_LIBS(gethostbyname, c nsl,, AC_MSG_ERROR(libnsl not found.))
AC_SEARCH_LIBS(socket, c socket,, AC_MSG_ERROR(libsocket not found.))
AC_SEARCH_LIBS(poll, c poll) 
AC_SEARCH_LIBS(gzopen,c z,,AC_MSG_ERROR(zlib not found.))

dnl
dnl  Do we have dn_expand ?
dnl

AC_SEARCH_LIBS(dn_expand, c resolv,
   AM_CONDITIONAL(HAVE_DN_EXPAND, true) ac_cv_ec_dns=yes, 
   AM_CONDITIONAL(HAVE_DN_EXPAND, false) ac_cv_ec_dns=no)

dnl ===============================
dnl   Check for non standard libs
dnl ===============================

EC_MESSAGE(Checking for missing functions)

AC_CONFIG_LIBOBJ_DIR(src/missing/)
ac_config_libobj_dir=src/missing

AM_CONDITIONAL(MISSING_STRLCPY, false)
AM_CONDITIONAL(MISSING_STRLCAT, false)
AM_CONDITIONAL(MISSING_STRSEP, false)
AM_CONDITIONAL(MISSING_MEMMEM, false)
AM_CONDITIONAL(MISSING_MEMCMP, false)
AM_CONDITIONAL(MISSING_GETOPT_LONG, false)
AM_CONDITIONAL(MISSING_STRCASESTR, false)
AM_CONDITIONAL(MISSING_INET_ATON, false)
AM_CONDITIONAL(MISSING_SCANDIR, false)

dnl AC_REPLACE_FUNCS(strlcpy)

AC_CHECK_FUNCS([strlcpy], , AC_LIBOBJ(missing/strlcpy) AM_CONDITIONAL(MISSING_STRLCPY, true) )
AC_CHECK_FUNCS([strlcat], , AC_LIBOBJ(missing/strlcat) AM_CONDITIONAL(MISSING_STRLCAT, true) )
AC_CHECK_FUNCS([strsep], , AC_LIBOBJ(missing/strsep) AM_CONDITIONAL(MISSING_STRSEP, true) )
AC_CHECK_FUNCS([memmem], , AC_LIBOBJ(missing/memmem) AM_CONDITIONAL(MISSING_MEMMEM, true) )
AC_CHECK_FUNCS([memcmp], , AC_LIBOBJ(missing/memcmp) AM_CONDITIONAL(MISSING_MEMCMP, true) )
AC_CHECK_FUNCS([getopt_long], , AC_LIBOBJ(missing/getopt) AM_CONDITIONAL(MISSING_GETOPT_LONG, true) )
AC_CHECK_FUNCS([strcasestr], , AC_LIBOBJ(missing/strcasestr) AM_CONDITIONAL(MISSING_STRCASESTR, true) )
AC_CHECK_FUNCS([scandir], , AC_LIBOBJ(missing/scandir) AM_CONDITIONAL(MISSING_SCANDIR, true) )
AC_CHECK_FUNCS([inet_aton], , AC_LIBOBJ(missing/inet_aton) AM_CONDITIONAL(MISSING_INET_ATON, true) )

AC_CHECK_LIB(resolv, inet_aton,
   AM_CONDITIONAL(INET_ATON_WANTS_RESOLV, true), 
   AM_CONDITIONAL(INET_ATON_WANTS_RESOLV, false))

dnl =======================================
dnl   Check user defined --enable-FEATURE
dnl =======================================

EC_MESSAGE(Checking user defined options)

dnl -----------
dnl -- DEBUG --
dnl -----------

AH_TEMPLATE(DEBUG, [debug support])

ac_cv_ec_debug=no
AC_MSG_CHECKING([if --enable-debug option was specified])
AC_ARG_ENABLE(debug, [  --enable-debug          create a file for debugging messages.],
[ case "$enableval" in
   yes)
      AC_DEFINE(DEBUG, 1)
      DEBUG_FLAGS="$CFLAGS -g -ggdb -Wmissing-prototypes -Werror -Wsign-compare -Wall"
      AC_MSG_RESULT(yes)
      ac_cv_ec_debug=yes
      ;;
   no)  AC_MSG_RESULT(no)
      DEBUG_FLAGS="$CFLAGS -O2 -funroll-loops -fomit-frame-pointer -Wall"
      ac_cv_ec_debug=no
      ;;
esac ],
   AC_MSG_RESULT(no)
   DEBUG_FLAGS="$CFLAGS -O2 -funroll-loops -fomit-frame-pointer -Wall"
   ac_cv_ec_debug=no
)

dnl -------------
dnl -- PLUGINS --
dnl -------------

AM_CONDITIONAL(PLUGINS, false)
AH_TEMPLATE(HAVE_PLUGINS, [plugins support])

AH_TEMPLATE(PERMIT_PLUGINS, [can ettercap use plugins ?])

check_plugins=0
ac_cv_ec_plugin=no
AC_MSG_CHECKING([if --enable-plugins option was specified])
AC_ARG_ENABLE(plugins, [  --enable-plugins        enable the use of plugins in ettercap.],
[ case "$enableval" in
   yes)
      AC_MSG_RESULT(yes)
      AM_CONDITIONAL(PLUGINS, true)
      AC_DEFINE(HAVE_PLUGINS, 1)
      ac_cv_ec_plugin=yes
      ;;
   no)  AC_MSG_RESULT(no)
      ac_cv_ec_plugin=no
      ;;
esac ],
   AC_MSG_RESULT(yes by default)
   AM_CONDITIONAL(PLUGINS, true)
   AC_DEFINE(HAVE_PLUGINS, 1)
   ac_cv_ec_plugin=yes
)

dnl ==================
dnl   Check for libs
dnl ==================


dnl ---------
dnl  libpcap
dnl ---------
ac_cv_ec_libpcap=default

AC_MSG_CHECKING(for libpcap)
AC_ARG_WITH(libpcap,
[  --with-libpcap=DIR      use libpcap in DIR],
[ case "$withval" in
  no)
     AC_MSG_RESULT(no)
     AC_ERROR("*** ettercap cannot operate without libpcap. ***")
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/pcap.h -a -f $withval/libpcap.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        PCAPINC="-I$withval"
        PCAPLIB="-L$withval -lpcap"
        ac_cv_ec_libpcap=$withval
     elif test -f $withval/include/pcap.h -a \
               -f $withval/include/net/bpf.h -a \
               -f $withval/lib/libpcap.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        PCAPINC="-I$withval/include"
        PCAPLIB="-L$withval/lib -lpcap"
        ac_cv_ec_libpcap=$withval
     else
        AC_ERROR(pcap.h, net/bpf.h, or libpcap.a not found in $withval)
     fi
     ;;
  esac ],
[ if test -f /usr/include/pcap/pcap.h; then
     PCAPINC="-I/usr/include/pcap"
     PCAPLIB="-lpcap"
  elif test -f /usr/include/pcap.h; then
     PCAPLIB="-lpcap"
  elif test -f /usr/local/include/pcap.h; then
     PCAPINC="-I/usr/local/include"
     PCAPLIB="-lpcap"
  else
     AC_MSG_RESULT(no)
     AC_ERROR(libpcap not found)
  fi
  AC_MSG_RESULT(yes) ]
)

OLDLDFLAGS="${LDFLAGS}"
LDFLAGS="$PCAPLIB"
AC_CHECK_LIB(pcap, pcap_file,, AC_ERROR(Incorrect pcap version. pcap >= 0.7.x required))
LDFLAGS="${OLDLDFLAGS}"

dnl ---------
dnl  libnet
dnl ---------
ac_cv_ec_libnet=default

AC_MSG_CHECKING(for libnet)
AC_ARG_WITH(libnet,
[  --with-libnet=DIR       use libnet in DIR],
[ case "$withval" in
  no)
     AC_MSG_RESULT(no)
     AC_ERROR("*** ettercap cannot operate without libnet. ***")
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/include/libnet.h \
             -a -f $withval/src/libnet.a \
             -o -f $withval/lib/libnet.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        if test -f $withval/bin/libnet-config; then
           LNETINC="`$withval/bin/libnet-config --defines`"
           LNETLIB="`$withval/bin/libnet-config --libs`"
        elif test -f $withval/libnet-config; then
           LNETINC="`$withval/libnet-config --defines`"
           LNETLIB="`$withval/libnet-config --libs`"
        else
           AC_PATH_PROG(LNET_CONFIG, "libnet-config")
           if test ! -n "$LNET_CONFIG"; then
              AC_ERROR(libnet-config not found)
           fi
           LNETINC="`libnet-config --defines`"
           LNETLIB="`libnet-config --libs`"
        fi
        LNETINC="$LNETINC -I$withval/include"
        LNETLIB="-L$withval/lib -L$withval/src $LNETLIB"
        ac_cv_ec_libnet=$withval
     else
        AC_ERROR(libnet.h or libnet.a not found in $withval)
     fi
     ;;
  esac ],
[ if test -f /usr/include/libnet.h; then
     AC_MSG_RESULT(yes)
     AC_PATH_PROG(LNET_CONFIG, "libnet-config")
     if test ! -n "$LNET_CONFIG"; then
        AC_ERROR(libnet-config not found)
     fi
     LNETINC="`libnet-config --defines`"
     LNETLIB="`libnet-config --libs`"
  else
     AC_MSG_RESULT(no)
     AC_ERROR(libnet >= 1.1.1 not found)
  fi
]
)

OLDLDFLAGS="${LDFLAGS}"
LDFLAGS="$LNETLIB"
AC_CHECK_LIB(net, libnet_init,, AC_ERROR(Incorrect lnet version. libnet >= 1.1.1 required))
AC_CHECK_LIB(net, libnet_autobuild_token_ring,, AC_ERROR(Incorrect lnet version. libnet >= 1.1.1 required))
LDFLAGS="${OLDLDFLAGS}"


dnl ---------
dnl Checks for openssl
dnl ---------

AH_TEMPLATE(HAVE_OPENSSL, [openssl executable path])
AM_CONDITIONAL(OPENSSL, false)
ac_cv_ec_libssl="NO"

AC_MSG_CHECKING(for openssl)
AC_ARG_WITH(openssl,
[  --with-openssl=DIR      use openssl in DIR],
[ case "$withval" in
  yes|no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/include/openssl/ssl.h \
             -a -f $withval/lib/libcrypto.a \
             -o -f $withval/libcrypto.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        SSLINC="-I$withval/include"
        SSLLIB="-L$withval -lcrypto"
        AC_DEFINE(HAVE_OPENSSL, 1)
        AM_CONDITIONAL(OPENSSL, true)
        ac_cv_ec_libssl=$withval
     elif test -f $withval/ssl/include/openssl/ssl.h -a \
               -f $withval/ssl/lib/libcrypto.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        SSLINC="-I$withval/ssl/include"
        SSLLIB="-L$withval/ssl/lib -lcrypto"
        AC_DEFINE(HAVE_OPENSSL, 1)
        AM_CONDITIONAL(OPENSSL, true)
        ac_cv_ec_libssl=$withval
     else
        AC_ERROR(ssl.h or libssl.a not found in $withval)
     fi
     ;;
  esac ],
[ if test -f /usr/include/openssl/ssl.h; then
     SSLLIB="-lcrypto"
     AC_DEFINE(HAVE_OPENSSL, 1)
     AM_CONDITIONAL(OPENSSL, true)
     AC_MSG_RESULT(yes)
     ac_cv_ec_libssl=default
  else
     AC_MSG_RESULT(no)
  fi]
)

AH_TEMPLATE(OPENSSL_PATH, [openssl executable path])

AC_PATH_PROGS(OPENSSL_PATH, openssl,, $PATH:/usr/local/ssl/bin:${prefix}/bin:$withval/bin:$withval/ssl/bin)
if test -n "$OPENSSL_PATH"; then
  AC_DEFINE_UNQUOTED(OPENSSL_PATH, "$OPENSSL_PATH")
  ac_cv_ec_openssl=$OPENSSL_PATH
else
  AC_DEFINE_UNQUOTED(OPENSSL_PATH, "")
fi


dnl ---------
dnl  libpcre
dnl ---------

AH_TEMPLATE(HAVE_PCRE, [perl compatible regex])

ac_cv_ec_pcre=no

AC_MSG_CHECKING(for libpcre)
AC_ARG_WITH(libpcre,
[  --with-libpcre=DIR      use libpcre in DIR],
[ case "$withval" in
  no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/pcre.h \
             -a -f $withval/libpcre.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        PCREINC="-I$withval/"
        PCRELIB="-L$withval/"
        AM_CONDITIONAL(HAVE_PCRE, true) 
        AC_DEFINE(HAVE_PCRE, 1)
        ac_cv_ec_pcre=yes
     else
        AM_CONDITIONAL(HAVE_PCRE, false) 
     fi
     ;;
  esac ],
[ if test -f /usr/include/pcre.h; then
     AM_CONDITIONAL(HAVE_PCRE, true) 
     AC_DEFINE(HAVE_PCRE, 1)
     ac_cv_ec_pcre=yes
     PCREINC=""
     PCRELIB="-lpcre"
     AC_MSG_RESULT(yes)
  else
     AC_MSG_RESULT(no)
     AM_CONDITIONAL(HAVE_PCRE, false) 
  fi
]
)


dnl ---------
dnl  ncurses
dnl ---------
AH_TEMPLATE(HAVE_NCURSES, [ncurses support])
AM_CONDITIONAL(NCURSES, false)

ac_cv_ec_libncurses="NO"

AC_MSG_CHECKING(for libncurses)
AC_ARG_WITH(libncurses,
[  --with-libncurses=DIR   use libncurses in DIR],
[ case "$withval" in
  yes|no)
     AC_MSG_RESULT(no)
     ;;
  *)
     AC_MSG_RESULT($withval)
     if test -f $withval/ncurses.h -a -f $withval/libncurses.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        NCURSINC="-I$withval"
        NCURSLIB="-L$withval -lpanel -lmenu -lncurses"
        AC_DEFINE(HAVE_NCURSES, 1)
        AM_CONDITIONAL(NCURSES, true)
        ac_cv_ec_libncurses=$withval
     elif test -f $withval/include/ncurses.h \
               -o -f $withval/include/ncurses/ncurses.h \
               -a -f $withval/lib/libncurses.a; then
        owd=`pwd`
        if cd $withval; then withval=`pwd`; cd $owd; fi
        NCURSINC="-I$withval/include -I$withval/include/ncurses"
        NCURSLIB="-L$withval/lib -lpanel -lmenu -lncurses"
        AC_DEFINE(HAVE_NCURSES, 1)
        AM_CONDITIONAL(NCURSES, true)
        ac_cv_ec_libncurses=$withval
     else
        AC_ERROR(ncurses.h or libncurses.a not found in $withval)
     fi
     ;;
  esac ],
[ if test -f /usr/include/ncurses/ncurses.h; then
     NCURSINC="-I/usr/include/ncurses"
     NCURSLIB="-lpanel -lmenu -lncurses"
     AC_DEFINE(HAVE_NCURSES, 1)
     AM_CONDITIONAL(NCURSES, true)
     ac_cv_ec_libncurses=default
     AC_MSG_RESULT(yes)
  elif test -f /usr/include/ncurses.h; then
     NCURSLIB="-lpanel -lmenu -lncurses"
     AC_DEFINE(HAVE_NCURSES, 1)
     AM_CONDITIONAL(NCURSES, true)
     ac_cv_ec_libncurses=default
     AC_MSG_RESULT(yes)
  else
     AC_MSG_RESULT(no)
  fi]
)

dnl ---------
dnl  GTK
dnl ---------

AH_TEMPLATE(HAVE_GTK, [gtk+ support])
AM_CONDITIONAL(GTK, false)

ac_cv_ec_gtk="NO"

AC_MSG_CHECKING(if --enable-gtk option was specified)
AC_ARG_ENABLE(gtk, [  --enable-gtk            enable the gtk interface.],
[ case "$enableval" in
  no)
     AC_MSG_RESULT(no)
     ;;
  yes)
      AC_MSG_RESULT(yes)
      AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
      if test "$PKG_CONFIG" = "no" ; then
         AC_MSG_RESULT(pkg-config not found)
      else
         AC_MSG_CHECKING(for gtk+)
         PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.0.0 pango >= 1.0 atk >= 1.0,
            [ AC_DEFINE(HAVE_GTK, 1)
              AM_CONDITIONAL(GTK, true)
              AC_MSG_RESULT(yes)
              ac_cv_ec_gtk=yes ],
            [ AC_MSG_RESULT(no) ]
         )
      fi
     ;;
  esac ],
[ 
dnl      AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
dnl      if test "$PKG_CONFIG" = "no" ; then
dnl         AC_MSG_RESULT(pkg-config not found)
dnl      else
dnl         PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.0.0 pango >= 1.0 atk >= 1.0,
dnl            [ AC_DEFINE(HAVE_GTK, 1)
dnl              AM_CONDITIONAL(GTK, true)
dnl              ac_cv_ec_gtk=yes ]
dnl         )
dnl      fi

     AC_MSG_RESULT(no)
])


dnl -----------------------
dnl  set up the final vars
dnl -----------------------
EC_CFLAGS="$DEBUG_FLAGS $PCAPINC $LNETINC $SSLINC $PCREINC $NCURSINC $GTK_CFLAGS"
EC_LIBS="$LIBS $PCAPLIB $LNETLIB $SSLLIB $PCRELIB $NCURSLIB $GTK_LIBS $PTHREADLIB $SOCKETLIB"

AC_SUBST(EC_CFLAGS)
AC_SUBST(EC_LIBS)

dnl  for the plugins
PL_CFLAGS="$DEBUG_FLAGS"
AC_SUBST(PL_CFLAGS)

dnl ===============
dnl   FINISHED !!
dnl ===============

EC_MESSAGE(Writing output files)

AC_CONFIG_FILES(  Makefile
                  man/Makefile 
                  man/ettercap.8 
                  man/ettercap_plugins.8 
                  man/etterlog.8 
                  man/etterfilter.8 
                  man/etter.conf.5
                  share/Makefile 
                  src/Makefile 
                  src/os/Makefile 
                  src/interfaces/Makefile 
                  src/interfaces/daemon/Makefile 
                  src/interfaces/text/Makefile 
                  src/interfaces/curses/Makefile 
                  src/interfaces/curses/widgets/Makefile 
                  include/Makefile
                  utils/Makefile
                  utils/etterlog/Makefile
                  utils/etterfilter/Makefile
                  plug-ins/Makefile
                  plug-ins/dummy/Makefile
                  plug-ins/finger/Makefile
                  plug-ins/finger_submit/Makefile
                  plug-ins/link_type/Makefile
                  plug-ins/smb_clear/Makefile
                  plug-ins/smb_down/Makefile)

AC_OUTPUT()

dnl ===============
dnl   THE SUMMARY
dnl ===============

EC_MESSAGE(ettercap has been configured as follow)

echo "=================================================="
echo
   EC_CHECK_OPTION(" Install directory: ",${prefix})
echo
echo
   EC_CHECK_OPTION("Libraries :", "")
echo
   EC_CHECK_OPTION(" LIBPCAP ................ ",${ac_cv_ec_libpcap})
   EC_CHECK_OPTION(" LIBNET ................. ",${ac_cv_ec_libnet})
   EC_CHECK_OPTION(" LIBSSL ................. ",${ac_cv_ec_libssl})
   EC_CHECK_OPTION(" OPENSSL ................ ",${ac_cv_ec_openssl})
   EC_CHECK_OPTION(" NCURSES ................ ",${ac_cv_ec_libncurses})
   EC_CHECK_OPTION(" GTK+ ................... ",${ac_cv_ec_gtk})
echo
   EC_CHECK_OPTION("Functionalities :", "")
echo
   EC_CHECK_OPTION(" Debug mode ............. ",${ac_cv_ec_debug})
   EC_CHECK_OPTION(" Plugin support ......... ",${ac_cv_ec_plugin})
   EC_CHECK_OPTION(" Passive DNS ............ ",${ac_cv_ec_dns})
   EC_CHECK_OPTION(" Perl regex in filters .. ",${ac_cv_ec_pcre})
echo
echo "=================================================="
echo





dnl vim:ts=3:expandtab

