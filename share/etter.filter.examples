############################################################################
#                                                                          #
#  ettercap -- etter.filter.examples -- filter source file                 #
#                                                                          #
#  Copyright (C) 2001-2003  ALoR & NaGA                                    #
#                                                                          #
#  This program is free software; you can redistribute it and/or modify    #
#  it under the terms of the GNU General Public License as published by    #
#  the Free Software Foundation; either version 2 of the License, or       #
#  (at your option) any later version.                                     #
#                                                                          #
############################################################################

# make sure this filter will not be used...
exit();


# display a message if the tcp port is 22
if (ip.proto == TCP && tcp.port == 22) {
   msg("SSH packet");
}


# log all telnet traffic, also execute ./program on every packet
if (ip.proto == TCP && tcp.port == 23) {
   log(DATA.data, "./logfile.log");
   exec("./program");
}


# some operation on the payload of the packet
if ( DATA.data + 20 == 0x4142 ) {
   DATA.data + 20 = 0x4243;
} else {
   DATA.data + 20 = 0x4445;
}


# drop any packet containing "ettercap"
if (search(DATA.disp_data, "ettercap")) {
   msg("some one is talking about us...");
   drop();
}


# log ssh decrypted packets matching the regexp
if (ip.proto == TCP && tcp.port == 22) {
   if (regex(DATA.disp_data, ".*login.*")) {
      log(DATA.disp_data, "./decrypted_log");
   }
}

# eof

# vim:ts=3:expandtab
